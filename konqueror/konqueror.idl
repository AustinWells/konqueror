// -*-idl-*-
#ifndef __konqueror_idl__
#define __konqueror_idl__

#include <kom.idl>
#include <openparts.idl>
#include <openparts_ui.idl>

module Konqueror
{

  typedef sequence<string> StrList;

  struct URLRequest
  {
    string url;
    boolean reload /* = false */ ;
    long xOffset /* = 0 */ ;
    long yOffset /* = 0 */ ;
  };
    
  struct Transfer
  {
    string source;
    string destination;
  };
  typedef Transfer EventNewTransfer;
  const string eventNewTransfer = "Konqueror/NewTransfer";

  /*
   * This event is mapped by Konqueror's implementation of the MainView
   * interface as well as by all the views.
   * (we use an event to open an URL instead of a simple method openURL (or
   * similar) because this way you can install your own custom URL filters :-)
   */
  typedef URLRequest EventOpenURL;
  const string eventOpenURL = "Konqueror/OpenURL";

  interface View : OpenParts::Part
  {
    struct MenuPopupRequest
    {
      long x;
      long y;
      StrList urls;
      long mode;      
      boolean isLocalFile;      
    };

    struct HistoryEntry
    {
      string url;
      any data;
    };
    
    /*
     * A full featured Konqueror View has to map the eventOpenURL event (which 
     * has a structure EventOpenURL as parameter)
     *
     * In addition it may implement/emit the following signals:
     *      - signal void openURL( in URLRequest url );
     *      - signal void started( in OpenParts::Id id, in string url );
     *      - signal void completed( in OpenParts::Id id );
     *      - signal void canceled( in OpenParts::Id id );
     *      - signal void setStatusBarText( in string text );
     *      - signal void setLocationBarURL( in OpenParts::Id id, in string url );
     *      - signal void createNewWindow( in string url );
     *      - signal void popupMenu( in MenuPopupRequest popup );
     */
     
    string url();
    
    void stop();

    /*
     * Note: Call saveState() only if the view signalled that it's finished
     *       (signal "completed")
     */
    HistoryEntry saveState();
    void restoreState( in HistoryEntry history );
    
    /*
     * returns a name for the view (for example just like "KMailFolderView" or
     * "KonquerorHTMLView")
     */
    string viewName();

    /*
     * The two below defined events can be mapped by a view to provide view
     * specific GUI elements.
     * note: If the menu/toolbar provided in the event strcture is nil this
     * means that the gui element (menu/toolbar) is meant to be cleared and
     * you should free all references to any related menus/toolbars.
     */

    struct EventCreateViewMenu
    {
      OpenPartsUI::Menu menu;
      boolean create; // create items if true, cleanup if false
    };

    struct EventCreateViewToolBar
    {
      OpenPartsUI::ToolBar toolBar;
    };
  
    const string eventCreateViewMenu = "Konqueror/GUI/createViewMenu";
    const string eventCreateViewToolBar = "Konqueror/GUI/createViewToolBar";
  };

  typedef sequence<View> ViewList;
  
  /*
   * see kio_interface.h in libkio for further information
   */
  struct UDSAtom
  {
    string m_str;
    long m_long;
    unsigned long m_uds;
  };
  typedef sequence<UDSAtom> UDSEntry;
  
  interface KfmIconTreeViewItem
  {
    string url();
    
    boolean isMarked();
    void mark();
    void unmark();
    
    UDSEntry udsEntry();
    
    boolean acceptsDrops( in StrList formats );
    string mimeType();
  };
  
  interface KfmIconViewItem : KfmIconTreeViewItem
  {
  };
  
  interface KfmIconView : View
  {
//    typedef sequence<IconViewItem> IconViewItemList;
//    IconViewItemList selectedItems();
//    void updateDirectory();

    slot void slotShowDot();
    slot void slotLargeIcons();
    slot void slotSmallIcons();
  };

  interface KfmTreeViewItem : KfmIconTreeViewItem
  {
    string name();
  };
    
  interface KfmTreeView : View
  {
    slot void slotShowDot();
    slot void slotReloadTree();
  };
  
  interface HTMLView : View
  {
  
    struct SavedState
    {
      string frameName;
      boolean isFrame;
      long scrolling;
      long frameBorder;
      long marginWidth;
      long marginHeight;
      boolean allowResize;
      boolean isFrameSet;
      string url;
      string title;
      long xOffset;
      long yOffset;
      boolean hasFormsList;
      StrList forms;
      boolean hasFrameLayout;
      string fl_rows;
      string fl_cols;
      long fl_frameBorder;
      boolean fl_allowResize;
      boolean hasFrames;
      sequence<SavedState> frames;
    };
  
    slot void saveDocument();
    slot void saveFrame();
  };
  
  interface PartView : View
  {
    void setPart( in OpenParts::Part part );
    OpenParts::Part part();
    
    slot void detachPart();
  };

  interface TxtView : View
  {
  };
  
  enum NewViewPosition { above, below, left, right };

  interface MainView : OpenParts::Part
  {
    // Position is relative to activeView(); above and below create a new row
    void insertView( in View view, in NewViewPosition newViewPosition);
    void setActiveView( in OpenParts::Id id );
    View activeView();
    ViewList viewList();
    void removeView( in OpenParts::Id id );

    slot void openURL( in URLRequest url );    
    
    slot void setStatusBarText( in string text );
    slot void setLocationBarURL( in OpenParts::Id id, in string url );
    
    slot void createNewWindow( in string url );
    slot void popupMenu( in View::MenuPopupRequest popup );

    // File menu
    slot void slotNewWindow();
    slot void slotRun();
    slot void slotTerminal();
    slot void slotOpenLocation();
    slot void slotToolFind();
    slot void slotPrint();
    slot void slotClose();

    // Edit menu
    slot void slotCopy();
    slot void slotPaste();
    slot void slotTrash();
    slot void slotDelete();
    slot void slotSelect();
    slot void slotSelectAll();
    slot void slotSaveGeometry();

    // View menu
    slot void slotSplitView();
    slot void slotRowAbove();
    slot void slotRowBelow();
    slot void slotRemoveView();
    slot void slotShowHTML();
    slot void slotLargeIcons();
    slot void slotSmallIcons();
    slot void slotTreeView();
    slot void slotReload();
    slot void slotStop();

    // Go menu
    slot void slotUp();
    slot void slotBack();
    slot void slotForward();
    slot void slotHome();
    slot void slotShowCache();
    slot void slotShowHistory();
    slot void slotEditMimeTypes();
    slot void slotEditApplications();

    // Options menu
    slot void slotConfigureKeys();

    // Help menu
    slot void slotAboutApp();    
    slot void slotHelp();

    slot void slotURLEntered();

    slot void slotFileNewActivated( in long id );
    slot void slotFileNewAboutToShow();	
  
    slot void slotBookmarkSelected( in long id );
    slot void slotEditBookmarks();

    slot void slotURLStarted( in OpenParts::Id id, in string url );
    slot void slotURLCompleted( in OpenParts::Id id );
  };
  
  interface MainWindow : OpenParts::MainWindow
  {
    MainView mainView();
  };

  interface ViewFactory
  {
    View create();
  };
    
  interface Application : OpenParts::Application
  {
    MainWindow createMainWindow( in string url );
    MainView createMainView(); // without mainwindow, this is nonsense
    
    KfmIconView createKfmIconView();
    HTMLView createHTMLView();
    KfmTreeView createKfmTreeView();
    PartView createPartView();
    TxtView createTxtView();
  };
  
};

#endif
